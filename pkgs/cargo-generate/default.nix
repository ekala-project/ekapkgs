{
  lib,
  rustPlatform,
  fetchFromGitHub,
  pkg-config,
  libgit2,
  openssl,
  gitMinimal,
}:

rustPlatform.buildRustPackage (finalAttrs: {
  pname = "cargo-generate";
  version = "0.23.9";

  src = fetchFromGitHub {
    owner = "cargo-generate";
    repo = "cargo-generate";
    tag = "v${finalAttrs.version}";
    hash = "sha256-myakseKwLEjQa9GgcxdWZJWdjMFXh7Wi64CwrN2ZwSU=";
  };

  cargoHash = "sha256-IAUgpk3HD9znLORCQKNw+AO6NG9GEVWMU/cez3B+CKc=";

  nativeBuildInputs = [ pkg-config ];

  buildInputs = [
    libgit2
    openssl
  ];

  nativeCheckInputs = [ gitMinimal ];

  buildNoDefaultFeatures = true;

  preCheck = ''
    export HOME=$(mktemp -d) USER=nixbld
    git config --global user.name Nixbld
    git config --global user.email nixbld@localhost.localnet
  '';

  checkFlags = [
    "--skip=favorites::favorites_default_to_git_if_not_defined"
    "--skip=git_instead_of::should_read_the_instead_of_config_and_rewrite_an_git_at_url_to_https"
    "--skip=git_instead_of::should_read_the_instead_of_config_and_rewrite_an_ssh_url_to_https"
    "--skip=git_over_ssh::it_should_retrieve_the_private_key_from_ssh_agent"
    "--skip=git_over_ssh::it_should_support_a_public_repo"
    "--skip=git_over_ssh::it_should_use_a_ssh_key_provided_by_identity_argument"
    "--skip=hooks_and_rhai::it_fails_when_a_system_command_returns_non_zero_exit_code"
  ];

  env = {
    LIBGIT2_NO_VENDOR = 1;
  };

  meta = {
    description = "Tool to generate a new Rust project by leveraging a pre-existing git repository as a template";
    mainProgram = "cargo-generate";
    homepage = "https://github.com/cargo-generate/cargo-generate";
    changelog = "https://github.com/cargo-generate/cargo-generate/blob/v${finalAttrs.version}/CHANGELOG.md";
    license = with lib.licenses; [
      asl20
      mit
    ];
    maintainers = [ ];
  };
})
