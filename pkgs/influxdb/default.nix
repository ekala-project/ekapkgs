{
  lib,
  buildGo126Module,
  fetchFromGitHub,
  pkg-config,
  rustPlatform,
}:

let
  libflux_version = "0.196.1";

  flux = rustPlatform.buildRustPackage (finalAttrs: {
    pname = "libflux";
    version = libflux_version;
    src = fetchFromGitHub {
      owner = "influxdata";
      repo = "flux";
      tag = "v${libflux_version}";
      hash = "sha256-935aN2SxfNZvpG90rXuqZ2OTpSGLgiBDbZsBoG0WUvU=";
    };
    patches = [
      ./fix-unsigned-char.patch
    ];

    postPatch = ''
      substituteInPlace flux-core/src/lib.rs \
        --replace-fail "deny(warnings, missing_docs))]" "deny(warnings), allow(dead_code, mismatched_lifetime_syntaxes, unused_assignments))]"
    '';
    sourceRoot = "${finalAttrs.src.name}/libflux";

    cargoHash = "sha256-A6j/lb47Ob+Po8r1yvqBXDVP0Hf7cNz8WFZqiVUJj+Y=";
    nativeBuildInputs = [ rustPlatform.bindgenHook ];
    pkgcfg = ''
      Name: flux
      Version: ${libflux_version}
      Description: Library for the InfluxData Flux engine
      Cflags: -I/out/include
      Libs: -L/out/lib -lflux -lpthread
    '';
    postInstall = ''
      mkdir -p $out/include $out/pkgconfig
      cp -r $NIX_BUILD_TOP/${finalAttrs.src.name}/libflux/include/influxdata $out/include
      printf "%s" "$pkgcfg" > $out/pkgconfig/flux.pc
      substituteInPlace $out/pkgconfig/flux.pc \
        --replace-fail /out $out
    '';

    __structuredAttrs = true;
  });
in
buildGo126Module (finalAttrs: {
  pname = "influxdb";
  version = "1.12.2";

  src = fetchFromGitHub {
    owner = "influxdata";
    repo = "influxdb";
    tag = "v${finalAttrs.version}";
    hash = "sha256-Q05mKmAXxrk7IVNxUD8HHNKnWCxmNCdsr6NK7d7vOHM=";
  };

  vendorHash = "sha256-+6fOq/2YVz74Loy1pVLVRTr4OQm/fEBNtHy3+FQn51A=";

  nativeBuildInputs = [ pkg-config ];

  env.PKG_CONFIG_PATH = "${flux}/pkgconfig";

  preBuild = ''
    flux_ver=$(grep github.com/influxdata/flux go.mod | awk '{print $2}')
    if [ "$flux_ver" != "v${libflux_version}" ]; then
      echo "go.mod wants libflux $flux_ver, but nix derivation provides ${libflux_version}"
      exit 1
    fi
  '';

  doCheck = false;

  ldflags = [
    "-s"
    "-w"
    "-X main.version=${finalAttrs.version}"
  ];

  excludedPackages = "test";

  meta = {
    description = "Open-source distributed time series database";
    license = lib.licenses.mit;
    homepage = "https://influxdata.com/";
    maintainers = [ ];
  };
})
