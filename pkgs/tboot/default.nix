{
  lib,
  stdenv,
  fetchurl,
  openssl,
  perl,
  trousers,
  zlib,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "tboot";
  version = "1.12.2";

  src = fetchurl {
    url = "mirror://sourceforge/tboot/tboot-${finalAttrs.version}.tar.gz";
    hash = "sha256-MBIT6WUV0rUQ9DTKfIPqpYylwxBv6YLVebNPNDuOBHo=";
  };

  buildInputs = [
    openssl
    trousers
    zlib
  ];

  enableParallelBuilding = true;

  preConfigure = ''
    substituteInPlace tboot/Makefile --replace /usr/bin/perl ${perl}/bin/perl

    for a in lcptools-v2 tb_polgen utils; do
      substituteInPlace "$a/Makefile" --replace /usr/sbin /sbin
    done
    substituteInPlace docs/Makefile --replace /usr/share /share
  '';

  installFlags = [ "DESTDIR=$(out)" ];

  meta = {
    description = "Pre-kernel/VMM module that uses Intel(R) TXT to perform a measured and verified launch of an OS kernel/VMM";
    homepage = "https://sourceforge.net/projects/tboot/";
    changelog = "https://sourceforge.net/p/tboot/code/ci/v${finalAttrs.version}/tree/CHANGELOG";
    license = lib.licenses.bsd3;
    platforms = [
      "x86_64-linux"
      "i686-linux"
    ];
  };
})
